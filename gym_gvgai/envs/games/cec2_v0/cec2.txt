BasicGame key_handler=Pulse square_size=40
    SpriteSet
        floor > Immovable img=newset/floor2
        lightoff > Passive img=oryx/cspell1
        lighton > Passive img=oryx/cspell2
        avatar  > ShootAvatar stype=staff img=oryx/necromancer1
        staff > Flicker limit=1 img=oryx/staff2 shrinkfactor=0.8 singleton=True
        wall > Immovable img=oryx/wall3 autotiling=True

        ok > Immovable invisible=True hidden=True
        nook > Immovable invisible=True hidden=True

        transits > Immovable invisible=True hidden=True
            to_off >
            to_on >
            to_ok >
            to_ok2 >
            to_ok3 >
            to_nook >
            to_nook2 >
            to_nook3 >

    LevelMapping
        0 > lightoff floor
        1 > lighton floor

        a > lighton ok floor
        b > lightoff nook floor

        c > lighton nook floor
        d > lightoff ok floor

        A > floor avatar
        . > floor

    InteractionSet
        avatar wall > stepBack

        lighton staff > transformTo stype=to_off
        lightoff staff > transformTo stype=to_on
        # Give reward on transition, must prevent multiple transitions due too long flicker later
        ok staff > transformTo stype=to_nook scoreChange=-1
        nook staff > transformTo stype=to_ok scoreChange=1
        
        # Ingeniously programmed delay routine to prevent toggling because despite limit=1 the staff flickers too long
        to_ok3 floor > transformTo stype=ok
        to_ok2 floor > transformTo stype=to_ok3
        to_ok floor > transformTo stype=to_ok2
        to_nook3 floor > transformTo stype=nook
        to_nook2 floor > transformTo stype=to_nook3
        to_nook floor > transformTo stype=to_nook2

        avatar TIME > transformToAll stype=to_off stypeTo=lightoff timer=1 repeating=True
        avatar TIME > transformToAll stype=to_on stypeTo=lighton timer=1 repeating=True
        

    TerminationSet
        MultiSpriteCounter stype1=nook stype2=to_ok limit=0 win=True
        Timeout limit=1000 win=False
